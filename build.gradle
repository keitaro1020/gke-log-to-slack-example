group 'gke-log-service'
version '0.0.1'

buildscript {
    ext{
        kotlinVersion = '1.2.31'
        springBootVersion = "2.0.0.RELEASE"
    }

    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:+')
    }
}

apply plugin: "kotlin"
apply plugin: "kotlin-spring"
apply plugin: "org.springframework.boot"
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
    maven { url "https://repo.spring.io/milestone" }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dependencies {
    // springboot
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-jetty")

    // kotlin
    compile("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
    compile("org.jetbrains.kotlin:kotlin-reflect")

    // log
    compile 'com.google.cloud:google-cloud-logging-logback:0.42.1-alpha'
    compile 'org.eclipse.jetty.alpn:alpn-api:1.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    baseName = "${rootProject.name}"
    version =  "${version}"
}

docker {
    name "${project.group}/${jar.baseName}:${version}"
    files jar.archivePath
    buildArgs(['JAR_FILE': "${jar.archiveName}"])
}
